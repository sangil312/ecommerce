plugins {
	id 'java-library'
	id 'org.springframework.boot' version '3.5.8' apply(false)
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.asciidoctor.jvm.convert' version '4.0.4' apply(false)
}

ext {
	springCloudVersion = "2025.0.1"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

allprojects {
	group = 'com.dev'
	version = '0.0.1-SNAPSHOT'

	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.asciidoctor.jvm.convert'

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
		}
	}

	dependencies {
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
		testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'
		testImplementation 'io.rest-assured:spring-mock-mvc'
		annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
	}

	bootJar.enabled = false
	jar.enabled = true

	test {
		useJUnitPlatform {
		}
	}

	tasks.register('unitTest', Test) {
		group = 'verification'
		useJUnitPlatform {
			excludeTags('integration', 'restdocs')
		}
	}

	tasks.register('integrationTest', Test) {
		group = 'verification'
		useJUnitPlatform {
			includeTags('integration')
		}
	}

	tasks.register('restDocsTest', Test) {
		group = 'verification'
		useJUnitPlatform {
			includeTags('restdocs')
		}
	}

	tasks.named('asciidoctor') {
		inputs.dir snippetsDir
		dependsOn tasks.named('restDocsTest')
		attributes 'snippets': snippetsDir
	}

	ext {
		snippetsDir = file("build/generated-snippets")
	}
}

// 로컬 환경에서 docs 파일(API 문서) 생성 tasks
tasks.register('restDocsCopy', Copy) {
	dependsOn(':core:core-api:asciidoctor')
	from(project(':core:core-api').tasks.named('asciidoctor').map { it.outputDir })
	into "$rootDir/docs"
}

project(':core:core-api') {
	tasks.named('test') {
		finalizedBy tasks.named('asciidoctor')
	}
	tasks.named('restDocsTest') {
		finalizedBy rootProject.tasks.named('restDocsCopy')
	}

	bootJar.enabled = true
	jar.enabled = false
}
